#version 430 core

#ifndef GROUP_SIZE
#define GROUP_SIZE 512
#endif

layout (local_size_x = GROUP_SIZE) in;

layout (std430, binding = 0) buffer position_buffer {
	vec3 in_pos[];
};
layout (std430, binding = 1) buffer internal_cell_index_buffer {
	uint out_internal_cell_idx[];
};
layout (std430, binding = 2) buffer cell_count_buffer {
	uint in_out_cell_count[];
};

uniform uint  u_num_elements;
uniform vec3  u_inv_cell_ext;
uniform vec3  u_min_aabb;
uniform uvec3 u_cell_dim;

void main() {
	uint i = gl_GlobalInvocationID.x;
    if (i < u_num_elements) {
    	vec3 pos = in_pos[i] - u_min_aabb;
    	uvec3 cell_coord = uvec3(pos * u_inv_cell_ext);
    	uint cell_idx = u_cell_dim.x * u_cell_dim.y * cell_coord.z + u_cell_dim.x * cell_coord.y + cell_coord.x;
    	out_internal_cell_idx[i] = atomicAdd(in_out_cell_count[cell_idx], 1U);
	}
}